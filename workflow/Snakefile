import os
import sys
import numpy as np
import pandas as pd

##################################################
workflow_mode = config["workflow_mode"]
assert workflow_mode in {
    "bulk_genotyping",
    "single_cell_genotyping",
    "copytyping_preprocess",
}


# parse sample file
sample_df = pd.read_table(config["sample_file"])
required = ["SAMPLE", "REP_ID", "assay_type", "sample_type", "PATH_to_bam"]
optional = ["PATH_to_barcodes", "PATH_to_10x_ranger", "PATH_to_ref_annotations"]
missing = [c for c in required if c not in sample_df.columns]
assert not missing, f"missing columns: {missing}"
for c in optional:
    if c not in sample_df.columns:
        sample_df[c] = pd.NA
SAMPLE_ID = sample_df["SAMPLE"].iloc[0]
assert (sample_df["SAMPLE"] == SAMPLE_ID).all(), "identical SAMPLE required"
assert sample_df["REP_ID"].notna().all(), "REP_ID required for all replicates"
assert sample_df["PATH_to_bam"].notna().all(), "PATH_to_bam has missing values"

uniq_sample_types = sample_df["sample_type"].unique().tolist()
assert all(st in ["normal", "tumor"] for st in uniq_sample_types)

rep_ids = sample_df["REP_ID"].unique().tolist()
assay_types = sample_df["assay_type"].unique().tolist()

nonbulk_assays = {"scATAC", "scRNA", "VISIUM", "VISIUM3prime"}
bulk_assays = {"bulkDNA", "bulkWGS", "bulkWES"}
allowed_assay_types = list(bulk_assays) + list(nonbulk_assays)
assert all(dt in allowed_assay_types for dt in assay_types)
assay_type2modality = {
    "bulkDNA": "DNA",
    "bulkWGS": "DNA",
    "bulkWES": "DNA",
    "scATAC": "DNA",
    "scRNA": "RNA",
    "VISIUM": "RNA",
    "VISIUM3prime": "RNA",
}
assay_type2feature_type = {
    "bulkDNA": "unknown",
    "bulkWGS": "unknown",
    "bulkWES": "exon",
    "scATAC": "tile",
    "scRNA": "gene",
    "VISIUM": "gene",
    "VISIUM3prime": "gene",
}
sample_df["modality"] = sample_df["assay_type"].map(assay_type2modality)

if workflow_mode == "bulk_genotyping":
    sample_df = sample_df.loc[sample_df["assay_type"].isin(bulk_assays), :].reset_index(
        drop=True
    )
    assert len(sample_df) > 0, "no samples for bulk_genotyping mode"
    assert sample_df["REP_ID"].nunique() == len(
        sample_df
    ), "bulk mode must have no duplicated replicate IDs"
    assert sample_df["PATH_to_bam"].notna().all(), "PATH_to_bam has missing values"
    run_genotype_snps = True
elif workflow_mode == "single_cell_genotyping":
    sample_df = sample_df.loc[
        sample_df["assay_type"].isin(nonbulk_assays), :
    ].reset_index(drop=True)
    assert len(sample_df) > 0, "no samples for single_cell_genotyping mode"
    assert (
        sample_df["PATH_to_barcodes"].notna().all()
    ), "PATH_to_barcodes has missing values"
    assert (
        sample_df["PATH_to_10x_ranger"].notna().all()
    ), "PATH_to_10x_ranger has missing values"
    # validate per-replicate records
    for rep_id, rep_df in sample_df.groupby(by="REP_ID", sort=False):
        rep_assay_types = rep_df["assay_type"].unique().tolist()
        assert len(rep_assay_types) <= 2, "at most 2 data types allowed per replicate"
        if len(rep_assay_types) == 2:
            assert ("scRNA" in rep_assay_types) and ("scATAC" in rep_assay_types)
    run_genotype_snps = True
elif workflow_mode == "copytyping_preprocess":
    sample_df = sample_df.loc[
        sample_df["assay_type"].isin(nonbulk_assays), :
    ].reset_index(drop=True)
    assert len(sample_df) > 0, "no nonbulk for single_cell_genotyping mode"
    assert config.get("het_snp_vcf") is not None
    assert config.get("seg_ucn") is not None
    run_genotype_snps = False
else:
    raise ValueError(f"unknown mode: {workflow_mode}")

modalities = sample_df["modality"].unique().tolist()
normal_bams = sample_df.loc[
    sample_df["sample_type"] == "normal", "PATH_to_bam"
].to_list()
tumor_bams = sample_df.loc[sample_df["sample_type"] == "tumor", "PATH_to_bam"].to_list()

modality2bams = {}
for modality, rows in sample_df.groupby("modality", sort=False):
    modality2bams[modality] = rows["PATH_to_bam"].tolist()

assay2rep_ids = {}
assay2sample_types = {}
for assay_type, rows in sample_df.groupby("assay_type", sort=False):
    assay2rep_ids[assay_type] = rows["REP_ID"].tolist()
    assay2sample_types[assay_type] = rows["sample_type"].tolist()

for assay_type in allowed_assay_types:
    if assay_type not in assay2rep_ids:
        assay2rep_ids[assay_type] = []
        assay2sample_types[assay_type] = []

get_data = {}
rep2modalities = {}
rep2ref_annotation = {}
rep2isMultiome = {}
for rep_id, rep_df in sample_df.groupby(by="REP_ID", sort=False):
    rep2modalities[rep_id] = rep_df["modality"].tolist()
    ref_annotation_file = rep_df["PATH_to_ref_annotations"].tolist()[0]
    if not pd.isna(ref_annotation_file):
        rep2ref_annotation[rep_id] = str(ref_annotation_file)
    is_multiome = len(rep_df) > 1
    rep2isMultiome[rep_id] = is_multiome
    for _, row in rep_df.iterrows():
        assay_type = row["assay_type"]
        get_data[(assay_type, rep_id)] = [
            row["PATH_to_barcodes"],
            row["PATH_to_bam"],
            row["PATH_to_10x_ranger"],
        ]

if any(is_multiome for is_multiome in rep2isMultiome.values()) and len(rep_ids) > 1:
    print("genotyping >1 replicates involving multiome data, warning")

##################################################
# parse phase_snps inputs
refvers = config["reference_version"]
allowed_refvers = ["hg19", "hg38", "chm13v2"]
get_refvers2 = {"hg19": "b37", "hg38": "b38"}
assert refvers in allowed_refvers, "unsupported reference version"

phaser = config.get("phaser", "undefined")

# population-based phasing required to estimate switchprobs
require_genetic_map = run_genotype_snps and phaser not in ["longphase"]
if run_genotype_snps:
    # locate genetic map files, different phaser option
    if phaser == "eagle":
        assert config.get("eagle_dir") is not None
        gmap_dir = os.path.join(config["eagle_dir"], "tables")
        assert os.path.isdir(gmap_dir), "failed to locate gmap directory"
        get_genetic_map = lambda chrname: os.path.join(
            gmap_dir, f"genetic_map_{refvers}_withX.txt.gz"
        )
    elif phaser == "shapeit":
        assert config.get("shapeit_dir") is not None
        gmap_dir = os.path.join(config["shapeit_dir"], f"resources/maps")
        assert os.path.isdir(gmap_dir), "failed to locate gmap directory"
        refvers2 = get_refvers2[refvers]
        get_genetic_map = lambda chrname: os.path.join(
            gmap_dir, f"{refvers2}/chr{chrname}.{refvers2}.gmap.gz"
        )
    else:
        assert phaser in ["longphase"], f"unknown phaser: {phaser}"
    if phaser in ["eagle", "shapeit"]:
        assert all(
            os.path.exists(get_genetic_map(chrname))
            for chrname in config["chromosomes"]
        ), "failed to locate gmap files"
        phasing_panel = config["phasing_panel"]
        assert os.path.isdir(phasing_panel), "failed to locate phasing panel"
        get_phasing_panel = lambda chrname: os.path.join(
            phasing_panel, f"chr{chrname}.genotypes.bcf"
        )
        assert all(
            os.path.exists(get_phasing_panel(chrname))
            for chrname in config["chromosomes"]
        ), "failed to locate panel files"

##################################################
# final target files
pre_qc = False  # TODO add more files
final_targets = []
if workflow_mode == "copytyping_preprocess":
    for assay_type in assay_types:
        final_targets.append(config["allele_dir"] + f"/{assay_type}/cnv_segments.tsv")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/X_count.npz")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/Y_count.npz")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/D_count.npz")

else:
    for assay_type in assay_types:
        final_targets.append(config["allele_dir"] + f"/{assay_type}/sample_ids.tsv")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/bb.tsv.gz")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/bb.Tallele.npz")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/bb.Aallele.npz")
        final_targets.append(config["allele_dir"] + f"/{assay_type}/bb.Ballele.npz")
        if assay_type in bulk_assays:
            final_targets.append(config["allele_dir"] + f"/{assay_type}/bb.depth.npz")
            final_targets.append(config["allele_dir"] + f"/{assay_type}/bb.rdr.npz")
        else:
            final_targets.append(
                config["allele_dir"] + f"/{assay_type}/barcodes.tsv.gz"
            )


##################################################
# entrypoint
include: "rules/genotype_snps.smk"
include: "rules/phase_snps.smk"
include: "rules/pileup_snps.smk"
include: "rules/postprocess.smk"


rule all:
    input:
        final_targets,
    default_target: True
